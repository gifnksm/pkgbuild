# Maintainer: NAKASHIMA, Makoto <makoto.nksm@gmail.com>

pkgname=genie-systemd
_pkgname=genie
pkgver=1.32
pkgrel=1
pkgdesc="A quick way into a systemd \"bottle\" for WSL"
arch=('x86_64')
url="https://github.com/arkane-systems/genie"
depends=('daemonize' 'dotnet-runtime>=5.0' 'dotnet-host>=5.0' 'inetutils')
makedepends=('dotnet-sdk>=5.0')
conflicts=('genie-systemd')
provides=('genie-systemd')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "no-libexec.patch")
sha256sums=('d01a4045158a2d24f0990033e11361dd30168978902db40af30dea91a690f28a'
            '3564354d2ea7bbcc47e36c7161d43890438e39c469104da053d2119524414cb0')
options=(!strip)
backup=(etc/genie.ini)

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/no-libexec.patch"
}

build() {
  export DOTNET_CLI_TELEMETRY_OPTOUT=1
  export DOTNET_SKIP_FIRST_TIME_EXPERIENCE=true
  cd "${_pkgname}-${pkgver}"
  make -C genie build
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm 4755 -o root "genie/genie/bin/Release/net5.0/linux-x64/publish/genie" -t "${pkgdir}/usr/bin/"
  install -Dm 0755 -o root "genie/runinwsl/bin/Release/net5.0/linux-x64/publish/runinwsl" -t "${pkgdir}/usr/lib/genie/"
  install -Dm 0755 -o root "genie/scripts/10-genie-envar.sh" -t "${pkgdir}/usr/lib/genie/"
  install -d 0755 -o root "${pkgdir}/usr/lib/systemd/"{user,system}"-environment-generators/"
  ln -s "/usr/lib/genie/10-genie-envar.sh" "${pkgdir}/usr/lib/systemd/user-environment-generators/"
  ln -s "/usr/lib/genie/10-genie-envar.sh" "${pkgdir}/usr/lib/systemd/system-environment-generators/"
  install -Dm 0644 -o root "genie/conf/genie.ini" -t "${pkgdir}/etc/"
  install -Dm 0644 -o root "genie/docs/genie.8" -t "${pkgdir}/usr/share/man/man8/"
}
