# Maintainer: NAKASHIMA, Makoto <makoto.nksm@gmail.com>

pkgname=genie-systemd
_pkgname=genie
pkgver=1.39
pkgrel=1
pkgdesc="A quick way into a systemd \"bottle\" for WSL"
arch=('x86_64')
url="https://github.com/arkane-systems/genie"
license=('Unlicense')
depends=('daemonize' 'dotnet-runtime>=5.0' 'dotnet-host>=5.0')
makedepends=('dotnet-sdk>=5.0')
conflicts=('genie-systemd')
provides=('genie-systemd')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "no-libexec.patch")
sha256sums=('bc410aefdfaedc0b23d2babb242f4b2af26bbb3b366751bb2ff03f902c261915'
            '574d66cf866d7d52527ce346a5a15e531965ae2ed5296b6e588c764617546e29')
options=(!strip)
backup=(etc/genie.ini)

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/no-libexec.patch"
}

build() {
  export DOTNET_CLI_TELEMETRY_OPTOUT=1
  export DOTNET_SKIP_FIRST_TIME_EXPERIENCE=true
  cd "${_pkgname}-${pkgver}"
  make -C ./binsrc
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm 4755 -o root "binsrc/genie/bin/Release/net5.0/linux-x64/publish/genie" -t "${pkgdir}/usr/bin/"
  install -Dm 0755 -o root "binsrc/runinwsl/bin/Release/net5.0/linux-x64/publish/runinwsl" -t "${pkgdir}/usr/lib/genie/"
  install -Dm 0755 -o root "othersrc/scripts/10-genie-envar.sh" -t "${pkgdir}/usr/lib/genie/"
  install -dm 0755 -o root "${pkgdir}/usr/lib/systemd/"{user,system}"-environment-generators/"
  ln -s "/usr/lib/genie/10-genie-envar.sh" "${pkgdir}/usr/lib/systemd/user-environment-generators/"
  ln -s "/usr/lib/genie/10-genie-envar.sh" "${pkgdir}/usr/lib/systemd/system-environment-generators/"
  install -Dm 0644 -o root "othersrc/lib-systemd-system/wslg-xwayland.service" -t "${pkgdir}/usr/lib/systemd/system/"
  install -Dm 0644 -o root "othersrc/lib-systemd-system/wslg-xwayland.socket" -t "${pkgdir}/usr/lib/systemd/system/"
  install -Dm 0644 -o root "othersrc/lib-systemd-system/user-runtime-dir@.service.d/override.conf" -t "${pkgdir}/usr/lib/systemd/system/user-runtime-dir@.service.d/"
  install -Dm 0644 -o root "othersrc/etc/genie.ini" -t "${pkgdir}/etc/"
  install -Dm 0644 -o root "othersrc/docs/genie.8" -t "${pkgdir}/usr/share/man/man8/"
}
