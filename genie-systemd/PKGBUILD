# Maintainer: NAKASHIMA, Makoto <makoto.nksm@gmail.com>

pkgname=genie-systemd
_pkgname=genie
pkgver=1.41
pkgrel=1
pkgdesc="A quick way into a systemd \"bottle\" for WSL"
arch=('x86_64')
url="https://github.com/arkane-systems/genie"
license=('Unlicense')
depends=('daemonize' 'dotnet-runtime>=5.0' 'dotnet-host>=5.0')
makedepends=('dotnet-sdk>=5.0')
conflicts=('genie-systemd')
provides=('genie-systemd')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "no-libexec.patch")
sha256sums=('55ce6bdbe7664fd00b9acf72e81e34bf1e6a0ac42dc1883ce8b75d7239e36c9f'
            '301088c28db931085b8d418cc41995307378d37be4ce1c986e134c93e979a8b1')
options=(!strip)
backup=(etc/genie.ini)

prepare() {
  cd "${_pkgname}-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/no-libexec.patch"
}

build() {
  export DOTNET_CLI_TELEMETRY_OPTOUT=1
  export DOTNET_SKIP_FIRST_TIME_EXPERIENCE=true
  cd "${_pkgname}-${pkgver}"
  make -C ./binsrc
}

package() {
  cd "${_pkgname}-${pkgver}"

  # Binaries
  install -Dm 4755 -o root "binsrc/genie/bin/Release/net5.0/linux-x64/publish/genie" -t "${pkgdir}/usr/bin/"
  install -Dm 0755 -o root "binsrc/runinwsl/bin/Release/net5.0/linux-x64/publish/runinwsl" -t "${pkgdir}/usr/lib/genie/"

  # Environment generator
  install -Dm 0755 -o root "othersrc/scripts/80-genie-envar.sh" -t "${pkgdir}/usr/lib/genie/"

  # Runtime dir mapping
  install -Dm 0755 -o root "othersrc/scripts/map-user-runtime-dir.sh" -t "${pkgdir}/usr/lib/genie/"
  install -Dm 0755 -o root "othersrc/scripts/unmap-user-runtime-dir.sh" -t "${pkgdir}/usr/lib/genie/"

  # Configuration file
  install -Dm 0644 -o root "othersrc/etc/genie.ini" -t "${pkgdir}/etc/"

  # 80-genie-envvar.sh symlinks
  install -dm 0755 -o root "${pkgdir}/usr/lib/systemd/"{user,system}"-environment-generators/"
  ln -s "/usr/lib/genie/80-genie-envar.sh" "${pkgdir}/usr/lib/systemd/user-environment-generators/"
  ln -s "/usr/lib/genie/80-genie-envar.sh" "${pkgdir}/usr/lib/systemd/system-environment-generators/"

  # Unit files
  install -Dm 0644 -o root "othersrc/lib-systemd-system/wslg-xwayland.service" -t "${pkgdir}/usr/lib/systemd/system/"
  install -Dm 0644 -o root "othersrc/lib-systemd-system/wslg-xwayland.socket" -t "${pkgdir}/usr/lib/systemd/system/"
  install -dm 0755 -o root "${pkgdir}/usr/lib/systemd/system/sockets.target.wants"
  ln -s /usr/lib/systemd/system/wslg-xwayland.socket ${pkgdir}/usr/lib/systemd/system/sockets.target.wants/wslg-xwayland.socket
  install -Dm 0644 -o root "othersrc/lib-systemd-system/user-runtime-dir@.service.d/override.conf" -t "${pkgdir}/usr/lib/systemd/system/user-runtime-dir@.service.d/"

  # man page
  install -Dm 0644 -o root "othersrc/docs/genie.8" -t "${pkgdir}/usr/share/man/man8/"
}
