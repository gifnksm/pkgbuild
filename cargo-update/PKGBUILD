# Maintainer: NAKASHIMA, Makoto <makoto.nksm@gmail.com>

pkgname=cargo-update
pkgver=6.0.0
pkgrel=1
pkgdesc="A cargo subcommand for checking and applying updates to installed executables"
arch=( 'x86_64')
url="https://github.com/nabijaczleweli/cargo-update"
license=('MIT')
makedepends=('cargo' 'libssh2' 'openssl' 'ruby-ronn')
source=("https://github.com/nabijaczleweli/cargo-update/archive/v${pkgver}.tar.gz")
sha256sums=('92455bd6aeb0826a3cfb1864b98f2d837950b166be5de528d7ac29e07571b716')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo build --release
  ronn --roff ./man/*.md
}

package()
{
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "target/release/cargo-install-update" -t "${pkgdir}/usr/bin/"
  install -Dm755 "target/release/cargo-install-update-config" -t "${pkgdir}/usr/bin/"
  install -Dm644 "man/cargo-install-update.1" -t "${pkgdir}/usr/share/man/man1/"
  install -Dm644 "man/cargo-install-update-config.1" -t "${pkgdir}/usr/share/man/man1/"
  install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
