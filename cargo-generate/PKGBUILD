# Maintainer: NAKASHIMA, Makoto <makoto.nksm@gmail.com>

pkgname=cargo-generate
pkgver=0.7.1
pkgrel=1
pkgdesc='Use pre-existing git repositories as templates'
arch=('i686' 'x86_64')
url="https://github.com/$pkgname/$pkgname"
license=('APACHE' 'MIT')
depends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('aabd04d206c04cec84cdf3555eb569758491896783952faa998794a959d50c20')

build() {
	cd "$pkgname-$pkgver"
	cargo build --release
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
	install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
